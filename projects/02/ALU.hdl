// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   
   Not16(in=x, out=notx);
   Not16(in=y, out=noty);
   Mux4Way16(sel[1]=nx, sel[0]=zx  
            , a=x         // sel=00
            , b=false     // sel=01
            , c=notx      // sel=10
            , d=true      // sel=11
            , out=f2);    // output to operate on
   Mux4Way16(sel[1]=ny, sel[0]=zy  
            , a = y       // sel=00
            , b = false   // sel=01
            , c = noty    // sel=10
            , d = true    // sel=11
            , out=f4);    // output to operate on   
   
//   Mux16(a=x, b=false, sel=zx, out=f1); 	// 16-bit constant
   
//   Not16(in=f1, out=notX);   
//   Mux16(a=f1, b=notX, sel=nx, out=f2); 	// bitwise not

//   Mux16(a=y, b=false, sel=zy, out=f3); 	// 16-bit constant
   
//   Not16(in=f3, out=notY);   
//   Mux16(a=f3, b=notY, sel=ny, out=f4);    // bitwise not
   
   Add16(a=f2, b=f4, out=xPlusy);			// integer 2's complement addition
   And16(a=f2, b=f4, out=xAndy);			// bitwise and
   Mux16(a=xAndy, b=xPlusy, sel=f, out=f5);	
   
   Not16(in=f5, out=notf5);   
   Mux16(a=f5, b=notf5, sel=no, out=out, out[15]=ng, out[0..7]=outLSB, out[8..15]=outMSB);	// bitwise not
   
   Or8Way(in=outLSB, out=zrLSB);
   Or8Way(in=outMSB, out=zrMSB);
   Or(a=zrLSB, b=zrMSB, out=zrResult);
   Not(in=zrResult, out=zr);  // 1 if (out == 0), 0 otherwise
}